grammar org.xtext.unipampa.erdsl.ErDsl with org.eclipse.xtext.common.Terminals

generate erDsl "http://www.xtext.org/unipampa/erdsl/ErDsl"

ERModel:
	domain=Domain ';'
	('Entities{') entities+=Entity+ ('};')
	('Relationships{') relations+=Relation* ('};');
	
Domain:
	'Domain' name=ID;

Attribute:
  name=ID ':' type=DataType (isKey?='isIdentifier')?;

enum DataType:
	INT='int' | DOUBLE='double' | MONEY='money' | STRING='string' | BOOLEAN='boolean' | DATETIME='datetime' | BLOB='file';

Entity:
	name=ID ('is_a' isA+=[Entity])*
	('{' attributes+=Attribute (',' attributes+=Attribute)* '}')?;

Relation:
	(name=ID)? ('[' leftEnding=RelationSide 'is_Related_With' rightEnding=RelationSide ']') ('{'
	attributes+=Attribute (',' attributes+=Attribute)* '}')*;

RelationSide:
	((minimalCardinality?='zero')?) maximumCardinality=CardinalityType target=[Entity];
	/*| target=[Relation] AMBIGUIDADE, PRECISA ENCONTRAR OUTRA MANEIRA OU ASSUMIR UM FORMATO ESPECIAL PARA A RELACAO*/

enum CardinalityType:
	One='one' | Many='many';